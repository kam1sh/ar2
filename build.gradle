plugins {
    // Apply the Kotlin JVM plugin to add support for Kotlin.
    id 'org.jetbrains.kotlin.jvm' version "1.3.72"

    // for standalone jar
    id 'com.github.johnrengelman.shadow' version '5.2.0'

    // linter
    id "org.jmailen.kotlinter" version "2.3.0"

    id "org.jetbrains.kotlin.plugin.jpa" version "1.3.72"

    // Apply the application plugin to add support for building a CLI application.
    id 'application'

    id 'io.qameta.allure' version '2.8.1'
}

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
}

ext {
    koin_version = '2.0.1'
    http4k_version = '3.227.0'
    jackson_version = '2.10.1'
    targetJvm = '11'
    hibernate_version = '5.4.15.Final'
}

dependencies {
    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    // Use the Kotlin JDK 8 standard library.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    // Use the Kotlin test library.
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    // Use the Kotlin JUnit integration.
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation 'io.qameta.allure:allure-junit5:2.13.3'

    implementation group: "org.http4k", name: "http4k-core", version: "$http4k_version"
    implementation group: "org.http4k", name: "http4k-multipart", version: "$http4k_version"
    implementation group: "org.http4k", name: "http4k-server-undertow", version: "$http4k_version"
    implementation group: "org.http4k", name: "http4k-format-jackson", version: "$http4k_version"

    // jackson
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-yaml
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: "$jackson_version"
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jackson_version"
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: "$jackson_version"
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "$jackson_version"

    implementation "org.koin:koin-core:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    implementation group: 'at.favre.lib', name: 'bcrypt', version: '0.9.0'

    implementation group: 'com.github.ajalt', name: 'clikt', version: '2.3.0'

    // database
    implementation group: 'org.hibernate', name: 'hibernate-core', version: "$hibernate_version"
    implementation group: 'org.hibernate', name: 'hibernate-entitymanager', version: "$hibernate_version"
    implementation group: 'org.hibernate', name: 'hibernate-hikaricp', version: "$hibernate_version"
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.4.2'
    implementation 'org.postgresql:postgresql:42.2.9.jre7'
    // https://flywaydb.org/documentation/api/#download
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '6.2.0'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "$targetJvm"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "$targetJvm"
    }
}

test {
    dependsOn 'cleanTest'
    useJUnitPlatform()
}

allure {
    autoconfigure = true
    version = '2.13.3'
    useJUnit5 {
        version = '2.13.3'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

application {
    // Define the main class for the application.
    mainClassName = 'ar2.AppKt'
}
